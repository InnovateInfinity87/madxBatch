!-----------------------------------------------------------------------
! Nominal tracking for SPS slow extraction
!
! F.M. Velotti - francesco.maria.velotti@cern.ch
! L.S. Stoel - linda.susanne.stoel@cern.ch
!
!-----------------------------------------------------------------------

OPTION, INFO, WARN, -ECHO;
OPTION, RBARC=FALSE; ! The length of a rectangular magnet is the dist-
                     ! ance between the polefaces and not the arc length


!-----------------------------------------------------------------------
! Nominal settings for SFTPRO in SPS (2016)
!-----------------------------------------------------------------------

! Tune (on-resonance)
qh_res = 80.0 / 3.0; ! = 26.666...
qv = 26.58; ! NTS: Double check!! Also check difference between measurement and extraction qv

! Normalized chroma measured without bump, without extraction sextupoles
qh = 26.62;
dqh_norm = -0.770;
dqv_norm =  0.465;

! Extraction sextupoles (for knob=1)
extr_sext = -0.11992;

! Knob values
knob_extr_sext = 1.0;
knob_extr_bump = 0.945;

! Tune range for tracking
dpp_start = -0.0015;
dpp_end = 0.0015;
qh_start := 80/3 - dpp_start*chroma_res;
qh_end := 80/3 - dpp_end*chroma_res;

! ZS alignment
zswireup = 0.06795;
zswiredo = 0.04140;

! Using pycollimate?
use_pycoll = pyPYCOLL;
zswirethickness = 0.0002;


/**********************************************************************/
!-----------------------------------------------------------------------
! Call files for correct initialization of SPS.
!-----------------------------------------------------------------------

CALL, FILE = '/afs/cern.ch/eng/sps/2015/beams/cngs_extraction.beamx';
CALL, FILE = 'pyHOMEDIR/input/sps.ele';
CALL, FILE = 'pyHOMEDIR/input/aperturedb_1.dbx';
CALL, FILE = '/afs/cern.ch/eng/sps/2015/aperture/aperturedb_2.dbx';
CALL, FILE = '/afs/cern.ch/eng/sps/2015/sequence/sps.seq';
CALL, FILE = 'pyHOMEDIR/input/aperturedb_3.dbx';
CALL, FILE = '/afs/cern.ch/eng/sps/2015/strength/ft_noqs_ext.str';
OPTION, -ECHO;
CALL, FILE = 'pyHOMEDIR/input/elements.str';

USE, SEQUENCE = sps;

CALL, FILE = 'pyHOMEDIR/madx/op_inittune.cmdx';
CALL, FILE = 'pyHOMEDIR/madx/op_initchroma.cmdx';

! Initial values changed in matching later (values at tune measurement)
qh_setvalue = 26.62;
qv_setvalue = 26.58;
qph_setvalue = -1;
qpv_setvalue = 0.495;

!-----------------------------------------------------------------------
! Set extraction sextupoles
!-----------------------------------------------------------------------

klse10602 := extr_sext * knob_extr_sext;
klse22402 := extr_sext * knob_extr_sext;
klse40602 := extr_sext * knob_extr_sext;
klse52402 := extr_sext * knob_extr_sext;


!-----------------------------------------------------------------------
! Build sliceable extraction bump
!-----------------------------------------------------------------------

! Redefine bumpers as RBEND instead of KICKER
MPSH_rb.21202: RBEND, L = .714, angle=0.0, APERTYPE=RECTANGLE, APERTURE={0.076,0.01765};
MPLH_rb.21431: RBEND, L = 2.06, angle=0.0, APERTYPE=RECTANGLE, APERTURE={0.076,0.01765};
MPNH_rb.21732: RBEND, L = 2.04, angle=0.0, APERTYPE=ELLIPSE, APERTURE={0.078,0.028};
MPLH_rb.21995: RBEND, L = 2.06, angle=0.0, APERTYPE=RECTANGLE, APERTURE={0.076,0.01765};
MPLH_rb.22195: RBEND, L = 2.06, angle=0.0, APERTYPE=RECTANGLE, APERTURE={0.076,0.01765};

! Replace old bumpers by new
SEQEDIT, SEQUENCE = sps;
 FLATTEN;
 REPLACE, ELEMENT = MPSH.21202, BY = MPSH_rb.21202;
 REPLACE, ELEMENT = MPLH.21431, BY = MPLH_rb.21431;
 REPLACE, ELEMENT = MPNH.21732, BY = MPNH_rb.21732;
 REPLACE, ELEMENT = MPLH.21995, BY = MPLH_rb.21995;
 REPLACE, ELEMENT = MPLH.22195, BY = MPLH_rb.22195;
 FLATTEN;
ENDEDIT;
USE, SEQUENCE = sps;


/**********************************************************************/
!-----------------------------------------------------------------------
! Changes that must be done on the thick sequence should be done here.
!-----------------------------------------------------------------------

/*pyTHICKCHANGES*/


/**********************************************************************/
!-----------------------------------------------------------------------
! Thinning of the sequence, remove zs aperture if in pycollimate
!-----------------------------------------------------------------------

SELECT, FLAG = makethin, CLEAR;
SELECT, FLAG = makethin, CLASS = quadrupole, SLICE = 4;
SELECT, FLAG = makethin, CLASS = rbend, SLICE = 4;
SELECT, FLAG = makethin, CLASS = sextupole, SLICE = 1;

MAKETHIN, SEQUENCE = sps, STYLE = teapot;
USE, SEQUENCE = sps;

IF (use_pycoll==1){
 AP.UP.ZS21633_M: AP,APERTYPE=RECTANGLE,APERTURE={0.08+(0.02+zswirethickness)/2,0.078};
 AP.DO.ZS21633_M: MARKER;
 AP.UP.ZS21638_M: MARKER;
 AP.DO.ZS21638_M: MARKER;
 AP.UP.ZS21655_M: MARKER;
 AP.DO.ZS21655_M: MARKER;
 AP.UP.ZS21671_M: MARKER;
 AP.DO.ZS21671_M: MARKER;
 AP.UP.ZS21676_M: MARKER;
 AP.DO.ZS21676_M: AP,APERTYPE=RECTANGLE,APERTURE={0.08+(0.02+zswirethickness)/2,0.078};
 ZS.61676_M: MARKER;
 ZS.21633_M: MARKER;
 ZS.21638_M: MARKER;
 ZS.21671_M: MARKER;
 ZS.21676_M: MARKER;

 SEQEDIT, SEQUENCE = sps;
  FLATTEN;
  REPLACE, ELEMENT = AP.UP.ZS21633, BY = AP.UP.ZS21633_M;
  REPLACE, ELEMENT = AP.DO.ZS21633, BY = AP.DO.ZS21633_M;
  REPLACE, ELEMENT = AP.UP.ZS21638, BY = AP.UP.ZS21638_M;
  REPLACE, ELEMENT = AP.DO.ZS21638, BY = AP.DO.ZS21638_M;
  REPLACE, ELEMENT = AP.UP.ZS21655, BY = AP.UP.ZS21655_M;
  REPLACE, ELEMENT = AP.DO.ZS21655, BY = AP.DO.ZS21655_M;
  REPLACE, ELEMENT = AP.UP.ZS21671, BY = AP.UP.ZS21671_M;
  REPLACE, ELEMENT = AP.DO.ZS21671, BY = AP.DO.ZS21671_M;
  REPLACE, ELEMENT = AP.UP.ZS21676, BY = AP.UP.ZS21676_M;
  REPLACE, ELEMENT = AP.DO.ZS21676, BY = AP.DO.ZS21676_M;
  REPLACE, ELEMENT = ZS.61676, BY = ZS.61676_M;
  REPLACE, ELEMENT = ZS.21633, BY = ZS.21633_M;
  REPLACE, ELEMENT = ZS.21638, BY = ZS.21638_M;
  REPLACE, ELEMENT = ZS.21671, BY = ZS.21671_M;
  REPLACE, ELEMENT = ZS.21676, BY = ZS.21676_M;
  FLATTEN;
 ENDEDIT;
 USE, SEQUENCE = sps;
}


/**********************************************************************/
!-----------------------------------------------------------------------
! Match chroma to measurement
!-----------------------------------------------------------------------
knob_extr_sext_val = knob_extr_sext;
knob_extr_sext = 0;
CALL, FILE='pyHOMEDIR/madx/op_matchtunechroma.cmdx';
knob_extr_sext = knob_extr_sext_val;


/**********************************************************************/
!-----------------------------------------------------------------------
! Changes that can be done on the thin sequence should be done here,
! unless they need the bump/errors to be applied first.
!-----------------------------------------------------------------------

/*pyTHINCHANGES*/


/**********************************************************************/
!-----------------------------------------------------------------------
! Set bump and misalignments
!-----------------------------------------------------------------------
lss2bump(nom_knob_value, knob_x_value, knob_px_value) : MACRO = {
 SELECT, FLAG=error, CLEAR;
 SELECT, FLAG=error, PATTERN="MPSH_rb\.21202.*";
 EFCOMP, ORDER:=0, DKN=(7.6765e-5*nom_knob_value + 2.331922219e-05*(knob_x_value) + 9.919569698e-07*(knob_px_value))/4.0;

 SELECT, FLAG=error, CLEAR;
 SELECT, FLAG=error, PATTERN="MPLH_rb\.21431.*";
 EFCOMP, ORDER:=0, DKN=(-0.49e-3*nom_knob_value - 9.667304386e-06*(knob_x_value) + 4.725651517e-08*(knob_px_value))/4.0;

 SELECT, FLAG=error, CLEAR;
 SELECT, FLAG=error, PATTERN="MPNH_rb\.21732.*";
 EFCOMP, ORDER:=0, DKN=(-0.33309e-3*nom_knob_value + 4.661e-05*(knob_x_value) + 2.323426145e-06*(knob_px_value))/4.0;

 SELECT, FLAG=error, CLEAR;
 SELECT, FLAG=error, PATTERN="MPLH_rb\.21995.*";
 EFCOMP, ORDER:=0, DKN=(-0.2503e-3*nom_knob_value - 1.539615399e-05*(knob_x_value) - 4.997512885e-07*(knob_px_value))/4.0;

 SELECT, FLAG=error, CLEAR;
 SELECT, FLAG=error, PATTERN="MPLH_rb\.22195.*";
 EFCOMP, ORDER:=0, DKN=(0.35585e-3*nom_knob_value + 2.868784873e-05*(knob_x_value) + 9.041110549e-07*(knob_px_value))/4.0;
};

EXEC, lss2bump(knob_extr_bump, 0.0, 0.0)

!================================
! Misa in beam dump ! Doesn't fully agree with geode?
!================================
SELECT, FLAG=ERROR, CLEAR;
SELECT, FLAG= ERROR, PATTERN="TIDH.11795";
EALIGN, AREX=-0.00425, AREY=-0.00611;

SELECT, FLAG=ERROR, CLEAR;
SELECT, FLAG=ERROR, PATTERN="TIDV.11892";
EALIGN, AREY=-0.00194;

!================================
! Misa in Sextant 2 ! With ugly hardcoding for 4 slices
!================================

SELECT, FLAG=ERROR, CLEAR;
SELECT, FLAG=ERROR, PATTERN="^MBB\.21530.*1$";
EALIGN, AREX=0.005*0.626/6.26;

SELECT, FLAG=ERROR, CLEAR;
SELECT, FLAG=ERROR, PATTERN="^MBB\.21530.*2$";
EALIGN, AREX=0.005*2.295333/6.26;

SELECT, FLAG=ERROR, CLEAR;
IF (VERSION>0){SELECT, FLAG=ERROR, PATTERN="^MBB\.21530$";}
         ELSE {SELECT, FLAG=ERROR, PATTERN="^MBB\.21530_S$";}
EALIGN, AREX=0.005*3.13/6.26;

SELECT, FLAG=ERROR, CLEAR;
SELECT, FLAG=ERROR, PATTERN="^MBB\.21530.*3$";
EALIGN, AREX=0.005*3.964667/6.26;

SELECT, FLAG=ERROR, CLEAR;
SELECT, FLAG=ERROR, PATTERN="^MBB.21530.*4$";
EALIGN, AREX=0.005*5.634/6.26;

SELECT, FLAG=ERROR, CLEAR;
SELECT, FLAG=ERROR, PATTERN="^MBB\.21550.*1$";
EALIGN, AREX=0.005+0.005*0.626/6.26;

SELECT, FLAG=ERROR, CLEAR;
SELECT, FLAG=ERROR, PATTERN="^MBB\.21550.*2$";
EALIGN, AREX=0.005+0.005*2.295333/6.26;

SELECT, FLAG=ERROR, CLEAR;
IF (VERSION>0){SELECT, FLAG=ERROR, PATTERN="^MBB\.21550$";}
         ELSE {SELECT, FLAG=ERROR, PATTERN="^MBB\.21550_S$";}
EALIGN, AREX=0.005+0.005*3.13/6.26;

SELECT, FLAG=ERROR, CLEAR;
SELECT, FLAG=ERROR, PATTERN="^MBB\.21550.*3$";
EALIGN, AREX=0.005+0.005*3.964667/6.26;

SELECT, FLAG=ERROR, CLEAR;
SELECT, FLAG=ERROR, PATTERN="^MBB\.21550.*4$";
EALIGN, AREX=0.005+0.005*5.634/6.26;

SELECT, FLAG=ERROR, CLEAR;
SELECT, FLAG=ERROR, PATTERN="^MBA\.21570.*1$";
EALIGN, AREX=0.01+0.001*0.626/6.26;

SELECT, FLAG=ERROR, CLEAR;
SELECT, FLAG=ERROR, PATTERN="^MBA\.21570.*2$";
EALIGN, AREX=0.01+0.001*2.295333/6.26;

SELECT, FLAG=ERROR, CLEAR;
IF (VERSION>0){SELECT, FLAG=ERROR, PATTERN="^MBA\.21570$";}
         ELSE {SELECT, FLAG=ERROR, PATTERN="^MBA\.21570_S$";}
EALIGN, AREX=0.01+0.001*3.13/6.26;

SELECT, FLAG=ERROR, CLEAR;
SELECT, FLAG=ERROR, PATTERN="^MBA\.21570.*3$";
EALIGN, AREX=0.01+0.001*3.964667/6.26;

SELECT, FLAG=ERROR, CLEAR;
SELECT, FLAG=ERROR, PATTERN="^MBA\.21570.*4$";
EALIGN, AREX=0.01+0.001*5.634/6.26;

SELECT, FLAG=ERROR, CLEAR;
SELECT, FLAG=ERROR, PATTERN="^MBA\.21590.*1$";
EALIGN, AREX=0.011;

SELECT, FLAG=ERROR, CLEAR;
SELECT, FLAG=ERROR, PATTERN="^MBA\.21590.*2$";
EALIGN, AREX=0.011;

SELECT, FLAG=ERROR, CLEAR;
IF (VERSION>0){SELECT, FLAG=ERROR, PATTERN="^MBA\.21590$";}
         ELSE {SELECT, FLAG=ERROR, PATTERN="^MBA\.21590_S$";}
EALIGN, AREX=0.011;

SELECT, FLAG=ERROR, CLEAR;
SELECT, FLAG=ERROR, PATTERN="^MBA\.21590.*3$";
EALIGN, AREX=0.011;

SELECT, FLAG=ERROR, CLEAR;
SELECT, FLAG=ERROR, PATTERN="^MBA\.21590.*4$";
EALIGN, AREX=0.011;

SELECT, FLAG=ERROR, CLEAR;
SELECT, FLAG=ERROR, PATTERN="VVSB.21601";
EALIGN, AREX=0.011;

SELECT, FLAG=ERROR, CLEAR;
SELECT, FLAG=ERROR, PATTERN="TCE.21695";
EALIGN, AREX=0.01;

!================================
! Aperture alignment extraction channel
!================================
CALL, FILE = 'pyHOMEDIR/input/align_extraction.cmdx';


/**********************************************************************/
!-----------------------------------------------------------------------
! Changes that need the bump/errors on need to be made here.
! Caution: Any USE command will cancel any errors present!
!-----------------------------------------------------------------------

/*pyFINALCHANGES*/


/**********************************************************************/
!-----------------------------------------------------------------------
! Generate a thin twiss and save the chroma at resonance
! In case this is for input twiss only: save the twiss and exit
!-----------------------------------------------------------------------

qh = qh_res;
CALL, FILE='pyHOMEDIR/madx/op_matchtune.cmdx'; # NTS: matchtune or matchtune_h?

SELECT, FLAG=TWISS, CLEAR;
SELECT, FLAG=TWISS, COLUMN=NAME,S,BETX,ALFX,X,DX,PX,DPX,MUX,BETY,ALFY,Y,DY,PY,DPY,MUY,APER_1,APER_2,APER_3,APER_4;
TWISS;

chroma_res = TABLE(SUMM,DQ1);

/*pyTWISS
WRITE, TABLE=TWISS, FILE='pyDATADIR/twiss_res.tfs';
SELECT, FLAG=TWISS, CLEAR;
SELECT, FLAG=TWISS, COLUMN=NAME,KEYWORD,S,L,TILT,ANGLE,HKICK,VKICK,K0L,K0SL,K1L,K1SL,K2L,K2SL,K3L,K3SL,K4L,K4SL,K5L,K5SL;
WRITE, TABLE=TWISS, FILE='pyDATADIR/strengths_res.tfs';
SELECT, FLAG=ERROR, FULL;
ESAVE, FILE='pyDATADIR/errors_res.tfs';
pyTWISS*/


/**********************************************************************/
!-----------------------------------------------------------------------
! Tracking code is completed by the python files
!-----------------------------------------------------------------------

pyTUNESETUP

/*pyTWISS
nturns = pyNTURNS;
CREATE, TABLE=QSWEEP, COLUMN=nturns,qv_setvalue,qh_set_start,qh_set_end,kqf1_start,kqf1_end,kqd_start,kqd_end;
FILL, TABLE=QSWEEP;
WRITE, TABLE=QSWEEP, FILE='pyDATADIR/qsweep.tfs';

SELECT, FLAG=TWISS, CLEAR;
SELECT, FLAG=TWISS, COLUMN=NAME,S,BETX,ALFX,X,DX,PX,DPX,MUX,BETY,ALFY,Y,DY,PY,DPY,MUY,APER_1,APER_2,APER_3,APER_4;
pyTWISSINIT

EXIT;
pyTWISS*/


pyTRACKER
